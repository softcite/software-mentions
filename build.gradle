buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath group: 'net.researchgate', name: 'gradle-release', version: '2.6.0'
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.4.0'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.1'
        classpath "gradle.plugin.org.gretty:gretty:2.3.1"
    }
}


apply plugin: 'maven'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'org.gretty'

group = "org.grobid"
version = '0.5.6-SNAPSHOT'

description = """software-mentions"""

sourceCompatibility = 1.8
targetCompatibility = 1.8

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
    maven {
        url new File(rootProject.rootDir, "lib")
    }
}

ext {
    // treating them separately, these jars will be flattened into grobid-core.jar on installing,
    // to avoid missing dependencies from the projects that include grobid-core (see 'jar' taskin grobid-core)
    localLibs = ['org/dkpro/statistics/dkpro-statistics-agreement/2.2.0-SNAPSHOT/dkpro-statistics-agreement-2.2.0-SNAPSHOT.jar']
}

dependencies {

    // packaging local libs inside output jar
    compile fileTree(dir: new File(rootProject.rootDir, 'lib'), include: localLibs)

    //Grobid
    compile group: 'org.grobid', name: 'grobid-core', version: '0.5.6-SNAPSHOT'
    compile group: 'org.grobid', name: 'grobid-trainer', version: '0.5.6-SNAPSHOT'
    compile group: 'net.arnx', name: 'jsonic', version: '1.3.5'
    //compile group: 'org.dkpro.statistics', name: 'dkpro-statistics-agreement', version: '2.2.0-SNAPSHOT'

    //Logging
    compile group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.7.25'

    //Apache commons
    compile group: 'commons-pool', name: 'commons-pool', version: '1.4'
    compile group: 'commons-io', name: 'commons-io', version: '2.5'
    compile group: 'commons-logging', name: 'commons-logging', version: '1.1.1'
    compile group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.6'
    compile group: 'org.apache.httpcomponents', name: 'httpmime', version: '4.5.6'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.0.1'
    compile group: 'org.apache.commons', name: 'commons-collections4', version: '4.1'
    compile group: 'org.apache.commons', name: 'commons-csv', version: '1.5'

    compile group: 'com.google.guava', name: 'guava', version: '16.0.1'

    //Parsing xml/json
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.8.11'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.8.11'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.8.11'
    compile group: 'xom', name: 'xom', version: '1.2.5'
    compile group: 'javax.xml.bind', name: 'jaxb-api', version: '2.2.11'
    compile group: 'com.sun.xml.bind', name: 'jaxb-core', version: '2.2.11'
    compile group: 'com.sun.xml.bind', name: 'jaxb-impl', version: '2.2.11'


    compile group: 'javax.activation', name: 'activation', version: '1.1.1'

    //Jersey
    compile group: 'com.sun.jersey', name: 'jersey-client', version: '1.8'
    compile group: 'com.sun.jersey', name: 'jersey-server', version: '1.8'
    compile group: 'com.sun.jersey', name: 'jersey-core', version: '1.8'


    providedCompile group: 'javax.servlet', name: 'servlet-api', version: '2.4'
    providedCompile group: 'javax.servlet.jsp', name: 'jsp-api', version: '2.0'
    compile group: 'asm', name: 'asm', version: '3.3.1'
    compile group: 'org.jvnet', name: 'mimepull', version: '1.6'
    compile group: 'com.sun.jersey.contribs', name: 'jersey-multipart', version: '1.8'
    compile group: 'javax.ws.rs', name: 'jsr311-api', version: '1.1.1'
    compile group: 'org.semanticweb.yars', name: 'nxparser-parsers', version: '2.3.3'

    //Tests
    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'


}

configurations {
    compile.exclude group: "org.slf4j", module: "slf4j-jdk14"
}

configurations.all {
    resolutionStrategy {
        force 'xml-apis:xml-apis:1.4.01'
    }
}

test {
    exclude '**/**IntegrationTest**'
}

// Gretty configuration
gretty {
    httpPort = 8060
    contextPath = '/'
    servletContainer = "jetty9.4"
    webInfIncludeJarPattern = ''
    reloadOnClassChange = false
}

// return the default value if the property has not been specified in command line
ext.getArg = { propName, defaultVal ->
    return project.hasProperty(propName) ? project.getProperty(propName) : defaultVal;
}

war {
    classifier = 'war'
    enabled true

    from {
        project.configurations.compile.collect {
            it.isDirectory() ? [] : localLibs.contains(it.getName()) ? zipTree(it) : []
        }
    }
}

import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

task mainJar(type: ShadowJar, group: 'test') {
    zip64 true
    from sourceSets.main.output

    from {
        project.configurations.compile.collect {
            it.isDirectory() ? [] : localLibs.contains(it.getName()) ? zipTree(it) : []
        }
    }
}

shadowJar {
    classifier = 'onejar'
    zip64 true
    manifest {
        attributes 'Main-Class': 'org.grobid.core.main.batch.SoftwareMain'
    }

    from {
        project.configurations.compile.collect {
            it.isDirectory() ? [] : localLibs.contains(it.getName()) ? zipTree(it) : []
        }
    }
}

jar {
    dependsOn mainJar
    enabled false
}



artifacts {
    archives jar
    archives shadowJar
    archives war
}

task copyModels(type: Copy) {
    from "${rootDir}/resources/model"
    include "*.wapiti"
    into "${rootDir}/../grobid-home/models/software/"
}

//tasks.withType(JavaCompile) {
//    options.compilerArgs << "-Xlint:deprecation"
//    options.compilerArgs << "-Xlint:unchecked"
//}


wrapper {
    gradleVersion "4.6"
}

// Custom Tasks

// Run like: ./gradlew eval_software -PgH=/path/grobid/home
task(trainer, dependsOn: 'classes', type: JavaExec, group: 'training') {
    main = 'org.grobid.trainer.SoftwareTrainerRunner'
    classpath = sourceSets.main.runtimeClasspath
    args '0', 'software', getArg('gH', '../grobid-home'), getArg('b', '0.00001 20 0'), getArg('t', '1')
//    jvmArgs '-Xms2g', '-Xmx8g'
}

// Run like: ./gradlew train_software -PgH=/path/grobid/home 
task(train_software, dependsOn: 'classes', type: JavaExec, group: 'training') {
    main = 'org.grobid.trainer.SoftwareTrainer'
    classpath = sourceSets.main.runtimeClasspath
//    args '0', 'software', getArg('gH', '../grobid-home'), getArg('b', ''),
//    jvmArgs '-Xms2g', '-Xmx8g'
}

// Run like: ./gradlew eval_software -PgH=/path/grobid/home
task(eval_software, dependsOn: 'classes', type: JavaExec, group: 'evaluation') {
    main = 'org.grobid.trainer.SoftwareTrainerRunner'
    classpath = sourceSets.main.runtimeClasspath
    args '1', 'software', getArg('gH', '../grobid-home')
//    jvmArgs '-Xms2g', '-Xmx8g'
}

// Run like: ./gradlew eval_software_split -PgH=/path/grobid/home -Ps=0.8 -Pt=10
task(eval_software_split, dependsOn: 'classes', type: JavaExec, group: 'evaluation') {
    main = 'org.grobid.trainer.SoftwareTrainerRunner'
    classpath = sourceSets.main.runtimeClasspath
    args '2', 'software', getArg('gH', '../grobid-home'), getArg('s', 0.9), getArg('t', '0')
    jvmArgs '-Xms2g', '-Xmx8g'
}

// Run like: ./gradlew annotated_corpus_generator_csv -Ppdf=/path/input/pdf -Pcsv=path/csv -Poutput=/output/directory
task(annotated_corpus_generator_csv, dependsOn: 'classes', type: JavaExec, group: 'import') {
    main = 'org.grobid.trainer.AnnotatedCorpusGeneratorCSV'
    classpath = sourceSets.main.runtimeClasspath
    args getArg('pdf', ''), getArg('csv', ''), getArg('output', 'resources/dataset/software/corpus/')
    jvmArgs '-Xms2g', '-Xmx8g'
}

// Run like: ./gradlew export_corpus_json -Pinput=/path/input/in.tei.xml -Poutput=/path/output/out.json
task(export_corpus_json, dependsOn: 'classes', type: JavaExec, group: 'export') {
    main = 'org.grobid.trainer.org.grobid.trainer.ExportCorpusJson'
    classpath = sourceSets.main.runtimeClasspath
    args getArg('input', ''), getArg('output', '')
    jvmArgs '-Xms2g', '-Xmx8g'
}

